parser SpecParser {
    root = toplevel*;
    toplevel = parserBlock / lexerBlock;
    parserBlock = Parser Ident OpenBracket rule* CloseBracket;
    rule = Ident Equal expr1 Semi;
    expr1 = expr2 (Slash expr2)*;
    expr2 = expr3+;
    expr3 = expr4 (Asta / Plus / Ques)?;
    expr4 = (Amp / Excl)? atom;
    atom = OpenParen expr1 CloseParen / expressionBlock / Ident;
    lexerBlock = Lexer Ident OpenBracket lexerRule* CloseBracket;
    lexerRule = (Ignored? Token)? Ident Equal lexerExpr1 (Arrow OpenBracket CloseBracket)? Semi;
    lexerExpr1 = lexerExpr2 (Slash lexerExpr2)*;
    lexerExpr2 = lexerExpr3+;
    lexerExpr3 = lexerExpr4 (Asta / Plus / Ques)?;
    lexerExpr4 = (Amp / Excl)? lexerAtom;
    lexerAtom = OpenParen lexerExpr1 CloseParen / Dot / Dollar / Str / CharRange / Ident;
    expressionBlock = Expression OpenBracket (exprAtom / operatorGroup)* CloseBracket;
    exprAtom = Atom atom;
    operatorGroup = Operator Group OpenBracket operatorRule* CloseBracket;
    operatorRule = (Prefix / Infix / Postfix) Operator Ident;
}

lexer SpecLexer {
    token EOF = $;
    ignored token Spacing = " " / "\t" / "\r\n" / "\n";
    token Asta = "*";
    token Plus = "+";
    token Excl = "!";
    token Amp = "&";
    token Ques = "?";
    token Slash = "/";
    token Dot = ".";
    token Dollar = "$";
    token Arrow = "=>";
    token Equal = "=";
    token Semi = ";";
    token OpenBracket = "{";
    token CloseBracket = "}";
    token OpenParen = "(";
    token CloseParen = ")";
    token Parser = "parser";
    token Lexer = "lexer";
    token Ignored = "ignored";
    token Token = "token";
    token Expression = "expression";
    token Atom = "atom";
    token Prefix = "prefix";
    token Infix = "infix";
    token Postfix = "postfix";
    token Operator = "operator";
    token Group = "group";
    token Str = "\"" (!"\"" (EscapeSeq / .))+ "\"" => { token.value = text(); };
    token CharRange = "[" (!"]" (&(. "-") . "-" . / .))+ "]" => { token.value = text(); };
    EscapeSeq = "\\" ("\\" / "\"" / "r" / "n" / "t");
    token Ident = [a-zA-Z_] [a-zA-Z0-9_]* => { token.value = text(); };
}
